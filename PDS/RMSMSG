RMSMSG   CSECT
*/********************************************************************/
*/*                                                                  */
*/* Name:        RMSMSG                                              */
*/*                                                                  */
*/* Function:    ZRMS subsystem message manager                      */
*/*                                                                  */
*/* Input Regs:  R1  Parmlist                                        */
*/*              R11 ZSVT address                                    */
*/*              R13 Savearea address                                */
*/*              R14 Return address                                  */
*/*              R15 Entry address                                   */
*/*                                                                  */
*/* Regs Usage:  R10 Base address                                    */
*/*              R11 ZSVT address                                    */
*/*              R12 DSA  address                                    */
*/*              R13 Savearea address                                */
*/*                                                                  */
*/* Output Regs: R0  Reason code                                     */
*/*              R1  Output parameter                                */
*/*              R15 Return code                                     */
*/*                                                                  */
*/* Attributes:  TCB mode                                            */
*/*              Primary mode                                        */
*/*              Reentrant                                           */
*/*              Caller key                                          */
*/*              Caller state                                        */
*/*              Page-fixed                                          */
*/*              Write-protected                                     */
*/*              Dynamic-LPA resident                                */
*/*                                                                  */
*/* Copyright:   Ben Marino. 2021 - 2023 (unpublished)               */
*/*                                                                  */
*/********************************************************************/
*---------------------------CHANGE LOG--------------------------------*
*                                                                     *
* ID    Rel    Date       Author        Comments                      *
*---------------------------------------------------------------------*
* @01 | V1R1 | 09242022 | Ben Marino  | Initial version               *
* @02 | V1R1 | 03292023 | Ben Marino  | Serviceability enhancements   *
*     |      |          |             |                               *
*     |      |          |             |                               *
***********************************************************************
*--------+---------+---------+---------+---------+---------+---------+*
RMSMSG   AMODE 31
RMSMSG   RMODE ANY
         COPY  ZGBLS
         USING RMSMSG,R15              Entry
         STM   R14,R12,GPR@R14(R13)    save caller's regs
         B     WTOINIT                 branch
         ZMACS GEN=MID,DSECT=NO        module ID
*
WTOINIT  DS    0H
         DROP  R15
         LR    R10,R15                 base
         USING RMSMSG,R10
         LR    R4,R1                   protect parm reg
         USING ZSVT,R11
         CLC   ZSVTID,=CL4'ZSVT'       ID?
         BNE   WTOABEND                no, ABEND
*
**       Identify message caller
*
         XR    R2,R2
         IPK   ,                       entry protect key
         LR    R5,R2                   protect it
         LA    R6,255                  LSQA subpool
         SRL   R2,4                    shift out 4-bits
         LTR   R2,R2                   key zero caller?
         BZ    WTO0000                 yes
         LA    R6,230                  private subpool
*
**       Obtain dynamic storage area
*
WTO0000  DS    0H
         STORAGE OBTAIN,LOC=ANY,                                       +
               LENGTH=DSA@SIZE,SP=(6),KEY=(5),LINKAGE=SYSTEM
         LR    R12,R1
         USING DSA,R12
         LA    R3,DSA@SIZE             DSA size
         LR    R2,R1                   copy address
         XR    R15,R15                 zero
         MVCL  R2,R14                  scrub DSA
         ST    R13,GPR@BWD(,R1)        chain,
         ST    R1,GPR@FWD(,R13)        saveareas.
         LR    R13,R1                  set my savearea
         ST    R4,DSAIPAR              save input parm
         ST    R5,DSAEKEY              save caller key
         ST    R6,DSAESPN              save storage subpool
*
**       Locate requested WTO message
*
         L     R2,WTOTEND              table end-address
         L     R3,WTOTBLE              table start-address
         L     R1,0(,R4)               WTOID address
         MVC   DSAMID,0(R1)            copy WTO msgID
*
WTO0010  DS    0H
         CR    R3,R2                   last entry?
         BL    WTO0020                 no, test further
         MVC   DSARC,=AL4(RC8)         set error RC
         LH    R1,ZRMS11E              get text length
         LA    R1,4(,R1)               add ROUT/DESC codes
         BCTR  R1,0
         EX    R1,COPYMSG              copy message
         MVC   DSAWTXT+1(7),DSAMID     show error msg-ID
         B     WTO0600                 issue error msg
*
WTO0020  DS    0H
         CLC   DSAMID,4(R3)            MID match?
         BE    WTO0030                 yes, carry on
         LH    R1,0(,R3)               get text length
         LA    R1,3(,R1)               round up to next,
         N     R1,=XL4'FFFFFFFC'       fullword boundary
         LA    R3,4(R1,R3)             add DESC+ROUT codes
         B     WTO0010                 loop
*
WTO0030  DS    0H
         MVI   DSAWTO,BLANK
         MVC   DSAWTO+1(DSAWTOL-1),DSAWTO
         LH    R1,0(,R3)               get text length
         ST    R1,DSATXLN              save text length
         LA    R1,4(,R1)               add DESC+ROUT size
         BCTR  R1,0                    decrement for EXecute
         EX    R1,COPYWTO              copy WTO message
*
**       Resolve WTO message variables
*
         LA    R6,DSAWTXT              msg text address
         LH    R5,DSAWLEN              get text length
         SH    R5,=H'4'                subtract MCS flags
*
WTO0040  DS    0H
         TM    0(R4),ENDVL             end of parmlist?
         BNZ   WTO0500                 yes, issue target WTO
         LA    R4,4(,R4)               next var ptr
         L     R3,0(,R4)               var data address
*
WTO0050  DS    0H
         CLI   0(R6),C'%'              var ID found?
         BE    WTO0060                 yest test further
         CLI   0(R6),C'!'              var ID found?
         BE    WTO0060                 yest test further
         CLI   0(R6),C'@'              var ID found?
         BE    WTO0060                 yest test further
         LA    R6,1(,R6)               walk thru text
         BCT   R5,WTO0050              loop
         B     WTO0500                 issue WTO message
*
WTO0060  DS    0H
         LR    R1,R6                   copy var ID address
         MVC   DSAVARID,0(R6)          save var ID found
*
WTO0070  DS    0H
         CLC   DSAVARID,0(R6)          end of var ID found?
         BNE   WTO0080                 yes, compute var ID length
         LA    R6,1(,R6)               walk thru text
         BCT   R5,WTO0070              loop
*
WTO0080  DS    0H
         LR    R2,R6                   copy msg text address
         SLR   R2,R1                   compute var length
         CLI   DSAVARID,C'%'           text var ID?
         BNE   WTO0090                 no, test further
         BCTR  R2,0                    decrement for EXecute
         EX    R2,MOVEVAR              move var data to msg
         B     WTO0400                 loop
*
WTO0090  DS    0H
         CLI   DSAVARID,C'!'           hexadecimal var ID?
         BNE   WTO0300                 no, test further
         MVC   DSASRC,0(R3)            get var value
         ZMACS GEN=CNVX,DSAVAR,DSASRC,TR=TRANTAB
         LA    R15,2                   compare length
         CLR   R2,R15                  display 6-hex digits?
         BH    WTO0100                 no, test further
         MVC   0(2,R1),DSAVAR+6        move var data to msg
         B     WTO0400                 loop
*
WTO0100  DS    0H
         LA    R15,4                   compare length
         CLR   R2,R15                  display 4-hex digits,
         BH    WTO0200                 no, test further
         MVC   0(4,R1),DSAVAR+4        move var data to msg
         B     WTO0400                 loop
*
WTO0200  DS    0H
         MVC   0(8,R1),DSAVAR          move var data to msg
         B     WTO0400                 loop
*
WTO0300  DS    0H
         CLI   DSAVARID,C'@'           decimal var ID?
         BNE   WTO0400                 no, carry on
         L     R2,0(,R3)               get var value
         CVD   R2,DSADWRD              make decimal
         OI    DSADWRD+7,X'0F'         fix sign
         UNPK  DSAVAR,DSADWRD          unpack it
         MVC   0(8,R1),DSAVAR          move var data to msg
*
WTO0400  DS    0H
         LA    R6,1(,R6)               walk thru text
         BCT   R5,WTO0040              loop
*
**       Validity check compression
*
WTO0500  DS    0H
         TM    ZSVTFLG2,ZSVT@CMP       CMP=YES?
         BZ    WTO0600                 no
         ZCALL SR=COMPWTO              yes
*
**       Issue requested WTO message
*
WTO0600  DS    0H
         USING PSA,0
         ICM   R1,15,PSATOLD           SRB-mode caller?
         BZ    WTO0700                 yes
         SETLOCK TEST,TYPE=LOCAL,BRANCH=(HELD,WTO0700),            @02A+
               RELATED=('Test local lock status')
         MVC   DSAWSUB,ZSVTSUBN        insert subsystem name
         WTO   MF=(E,DSAWTO)           issue WTO message
         B     WTO0999                 exit
*
WTO0700  DS    0H
         MVC   DSAWSUB,ZSVTSUBN        insert subsystem name
         WTO   MF=(E,DSAWTO),          issue WTO message               +
               LINKAGE=BRANCH          branch entry
*
WTO0999  DS    0H
         ST    R15,DSARC               save RC
         ST    R0,DSARSN               save RSN
         ST    R1,DSAGR1               save EOM ID
*---------------------------------------------------------------------*
*        Exit Routine                                                 *
*--------+---------+---------+---------+---------+---------+---------+*
WTOEXIT  DS    0H
         L     R13,GPR@BWD(,R13)       caller savearea
         LA    R3,GPR@R15(,R13)        R15 slot address
         MVC   0(DSA@OPZL,R3),DSAOPAR  return output parms
         L     R2,DSAEKEY              caller key
         L     R3,DSAESPN              storage subpool
         STORAGE RELEASE,COND=NO,ADDR=(12),                            +
               LENGTH=DSA@SIZE,SP=(3),KEY=(2),LINKAGE=SYSTEM
         LM    R14,R12,GPR@R14(R13)    restore caller's regs
         BR    R14                     return
*---------------------------------------------------------------------*
*        ABEND Exit                                                   *
*--------+---------+---------+---------+---------+---------+---------+*
WTOABEND DS    0H
         ABEND &UCODE,DUMP,REASON=(R2)
*---------------------------------------------------------------------*
*        EXecuted Instructions                                        *
*--------+---------+---------+---------+---------+---------+---------+*
         DS    0H
MOVEVAR  MVC   0(*-*,R1),0(R3)
COPYWTO  MVC   DSAWTO(*-*),0(R3)
COPYMSG  MVC   DSAWTO(*-*),ZRMS11E
*---------------------------------------------------------------------*
*        Constants                                                    *
*--------+---------+---------+---------+---------+---------+---------+*
         DS    0F
WTOTEND  DC    A(WTOTBEND)             WTO table end-address
WTOTBLE  DC    A(WTOTABLE)             WTO table start-address
TRANTAB  DC    CL16'0123456789ABCDEF'  translate table
*---------------------------------------------------------------------*
*        Literals                                                     *
*--------+---------+---------+---------+---------+---------+---------+*
         LTORG ,
         DROP  ,
*/********************************************************************/
*/*                                                                  */
*/* Name:         COMPWTO                                            */
*/*                                                                  */
*/* Function:     Compress WTO message subroutine                    */
*/*                                                                  */
*/* Input Regs:   R11 ZSVT address                                   */
*/* Input Regs:   R13 savearea address                               */
*/*               R14 return address                                 */
*/*               R15 entry point address                            */
*/*                                                                  */
*/* Regs Usage:   R11 ZSVT address                                   */
*/*               R10 Base address                                   */
*/*               R12 DSA address                                    */
*/*               R13 Savearea address                               */
*/*                                                                  */
*/* Output Regs:  R0  Reason code                                    */
*/*               R1  Output parameter                               */
*/*               R15 Return code                                    */
*/*                                                                  */
*/********************************************************************/
*/-------+---------+---------+---------+---------+---------+---------*/
COMPWTO  DS    0H
         USING DSA,R12
         B     12(,R15)
         DC    CL8'COMPWTO'            PC routine
         STM   R14,R12,GPR@R14(R13)    save caller's regs
         LR    R10,R15                 set base address
         USING COMPWTO,R10
         LA    R1,DSASBSA              subroutine save area
         ST    R13,GPR@BWD(,R1)        chain,
         ST    R1,GPR@FWD(,R13)        save,
         LR    R13,R1                  areas.
*
         LA    R4,DSAXTXT              compress msg address
         LA    R3,DSAWTXT              WTO msg text address
         LH    R2,DSAWLEN              WTO text length
         SH    R2,=H'12'               minus MCS flags & msg prefix
         LA    R2,0(R2,R3)             WTO msg end-address
         MVC   DSAROUT,0(R2)           save ROUTCDE & DESC codes
         MVI   DSAXTXT,BLANK           clear WTO workarea
         MVC   DSAXTXT+1(DSAXTXTL-1),DSAXTXT
         B     COMP020                 carry on
*
COMP010  DS    0H
         CLI   0(R3),XZERO             end of text?
         BE    COMP035                 yes, test further
         CLI   0(R3),BLANK             blank?
         BNE   COMP020
         LR    R1,R3                   msg text address
         BCTR  R1,0                    subtract one
         CLI   0(R1),BLANK             previous char blank?
         BNE   COMP020                 no, copy char
         LA    R3,1(,R3)               next msg text address
         B     COMP030                 loop
*
COMP020  DS    0H
         MVC   0(1,R4),0(R3)           copy char
         LA    R3,1(,R3)               next msg text char
         LA    R4,1(,R4)               next msg text char
*
COMP030  DS    0H
         CLR   R3,R2                   all done?
         BL    COMP010                 no, loop
*
COMP035  DS    0H
         MVI   DSAWTXT,BLANK           clear WTO workarea
         MVC   DSAWTXT+1(DSAWTXTL-1),DSAWTXT
         LA    R2,DSAXTXT              msg text address
         SLR   R4,R2                   get compresed msg length
         BCTR  R4,0
         EX    R4,COMPCOPY             copy compressed msg text
*
         LH    R2,DSAWLEN              MCS flags & msg length
         LA    R3,DSAWTO               WTO msg address
         LA    R3,0(R2,R3)             end-of-text address
         MVC   0(4,R3),DSAROUT         insert ROUTCDE & DESC code
*
COMPEXIT DS    0H
         L     R13,GPR@BWD(,R13)       mainline savearea
         LM    R14,R12,GPR@R14(R13)    restore mainline regs
         BR    R14                     return
*---------------------------------------------------------------------*
*        EXecuted Instructions                                        *
*--------+---------+---------+---------+---------+---------+---------+*
         DS    0H
COMPCOPY MVC   DSAWTXT(*-*),DSAXTXT    copy msg text
*---------------------------------------------------------------------*
*        Literals                                                     *
*--------+---------+---------+---------+---------+---------+---------+*
         LTORG ,
         DROP  ,
*--------+---------+---------+---------+---------+---------+---------+*
**       WTO messages                                                 *
*--------+---------+---------+---------+---------+---------+---------+*
WTOTABLE DS    0F
ZRMS00I  WTO   'ZRMS00I %%%% Resource monitor subsystem initialization +
               in progress',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS01I  WTO   'ZRMS01I %%%% Resource monitor subsystem initialization +
               complete',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS02I  WTO   'ZRMS02I %%%% Security server class %%%%%%%%',          +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS03E  WTO   'ZRMS03E CSVDYLPA %%% failed; MOD=%%%%%%%% RC=!!!! RSN=!+
               !!!!!!! FL=!!!!',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS04E  WTO   'ZRMS04E 4-character subsystem name required',          +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS05E  WTO   'ZRMS05E %%%% Subsystem initialization failed',         +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS06E  WTO   'ZRMS06E Invalid %%% option specified',                 +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS07E  WTO   'ZRMS07E Token=%%%%%%%%%%%%%%%% %%%%%%%% failed; RC=!!!!+
               ',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS08I  WTO   'ZRMS08I %%%% Previous instance resources released',    +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS09E  WTO   'ZRMS09E ZSVT Storage obtain failed; SP=%%% RC=!!!!',   +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS10E  WTO   'ZRMS10E CPF Command prefix define failed; RC=!!!! RSN=!+
               !!!!!!!',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS11E  WTO   'ZRMS11E MID=%%%%%%% not found in message table',       +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS12I  WTO   'ZRMS12I Subsysytem %%% option set to %%%',             +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS13E  WTO   'ZRMS13E Subsysytem command invalid; %%%%%%%%%%%%%%%%', +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS14E  WTO   'ZRMS14E WTO Display failed; RC=!!!! ID=!!!!!!!!',      +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS15E  WTO   'ZRMS15E COMMAND AUTH ERROR; SAF=!!!! RC=!!!! RSN=!!!!!!+
               !! CMD=%%%%%%%%%%%%%%%%',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS16I  WTO   'ZRMS16I Subsystem trace data crumbs cleared',          +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS17E  WTO   'ZRMS17E Invalid %%%% option specified',                +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS18E  WTO   'ZRMS18E SVCUPDTE %%%%%%% failed; SVC=%%% RC=!!!!',     +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS19E  WTO   'ZRMS19E SERVER AUTH ERROR; SAF=!!!! RC=!!!! RSN=!!!!!!!+
               ! PROFILE=%%%%%%%%%%%%%%',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS20I  WTO   'ZRMS20I %%%% Service module %%%%%%%% added to dynamic-L+
               PA',ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS21I  WTO   'ZRMS21I *** available entry ***',                      +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS22E  WTO   'ZRMS22E Enter %%%% HELP for a list of valid commands', +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS23E  WTO   'ZRMS23E Command invalid; %%%%%%%%%%%%%%%%%%%%%%%%',    +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS24I  WTO   'ZRMS24I *** available entry ***',                      +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS25I  WTO   'ZRMS25I *** available entry ***',                      +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS26I  WTO   'ZRMS26I *** available entry ***',                      +
               ROUTCDE=(2,11),DESC=(4),MF=L
         DS    0F
ZRMS27I  WTO   'ZRMS27I *** available entry ***',                      +
               ROUTCDE=(2,11),DESC=(4),MF=L
*
**       SUBSYS DD ROUTCDE=11 WTO messages
*
         DS    0F
ZRMS70E  WTO   'ZRMS70E %%%%%%%% CALLER MONITORING NOT SUPPORTED; RC=!!+
               !! RSN=!!!!!!!!',ROUTCDE=11,MF=L
         DS    0F
ZRMS71E  WTO   'ZRMS73E MONITORING TERMINATED; FC=%%%%%%%% RC=!!!! RSN=+
               !!!!!!!!',ROUTCDE=11,MF=L
         DS    0F
ZRMS72E  WTO   'ZRMS72E %%%% STRUCTURE FAILED VALIDITY CHECKS',        +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS73I  WTO   'ZRMS73I *** available entry ***',                      +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS74I  WTO   'ZRMS74I *** available entry ***',                      +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS79E  WTO   'ZRMS79E CALLER ISSUED SVC ABNORMALLY TERMINATED',      +
               ROUTCDE=11,MF=L
*
**       SUBSYS DD ROUTCDE=11 WTO messages
*
         DS    0F
ZRMS80W  WTO   'ZRMS80W RUN=J OPTION NOT SUPPORTED; RUN=S DEFAULT USED'+
               ,ROUTCDE=11,MF=L
         DS    0F
ZRMS81E  WTO   'ZRMS81E %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'+
               ,ROUTCDE=11,MF=L
         DS    0F
ZRMS82E  WTO   'ZRMS82E INVALID SUBPOOL SPECIFIED; SP=%%%%',           +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS83E  WTO   'ZRMS83E INVALID %%%% SEPARATOR SPECIFIED',             +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS84E  WTO   'ZRMS84E %%%%%%%% STORAGE %%%%%%% FAILED; RC=!!!!',     +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS85E  WTO   'ZRMS85E DD=%%%%%%%% ALLOCATION GROUP FUNCTION FAILED', +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS86E  WTO   'ZRMS86E DD=%%%%%%%% UNALLOCATION GROUP FUNCTION FAILED'+
               ,ROUTCDE=11,MF=L
         DS    0F
ZRMS87E  WTO   'ZRMS87E INVALID PARAMETERS SEPARATOR SPECIFIED; "%"',  +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS88E  WTO   'ZRMS88E INVALID %%%% KEYWORD SPECIFIED',               +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS89E  WTO   'ZRMS89E REQUIRED %%%% KEYWORD NOT SPECIFIED',          +
               ROUTCDE=11,MF=L
*
**       SUBSYS DD ROUTCDE=11 WTO messages
*
         DS    0F
ZRMS90E  WTO   'ZRMS90E INVALID SVC SPECIFIED. SVC=%%%%%%%%',          +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS91I  WTO   'ZRMS91I MONITOR: %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%+
               %%%%%%%%%%%%%%%',ROUTCDE=11,MF=L
         DS    0F
ZRMS92I  WTO   'ZRMS92I MONITOR: SUBDD=%%%%%%%% LOGDD=%%%%%%%% RPT=% RU+
               N=% SMF=%',ROUTCDE=1,MF=L
         DS    0F
ZRMS93W  WTO   'ZRMS93W %%%% SUBSYSTEM SVC=%%% WILL NOT BE MONITORED', +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS94W  WTO   'ZRMS94W RESTRICTED SVC=%%% WILL NOT BE MONITORED',     +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS94E  WTO   'ZRMS94E UNKNOWN SVC SPECIFIED; SVC=%%%%%%%%',          +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS95E  WTO   'ZRMS95E JOBSTEP NOT AUTHORIZED TO ISSUE SVC=!!!! MACRO=+
               %%%%%%%%%%%%%%%%%%%%%%%%%%%',ROUTCDE=11,MF=L
         DS    0F
ZRMS96E  WTO   'ZRMS96E INVALID LOG DD SPECIFIED; DD=%%%%%%%%',        +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS97E  WTO   'ZRMS97E INVALID %%%%%%%% OPTION SPECIFIED',            +
               ROUTCDE=11,MF=L
         DS    0F
ZRMS98E  WTO   'ZRMS98E SMF=Y OPTION IS INVALID WITH SMF=NO INITIALIZAT+
               ION OPTION SPECIFIED',ROUTCDE=11,MF=L
         DS    0F
ZRMS99E  WTO   'ZRMS99E ZRMS SUBSYS ERROR; nnnnnnn ABEND=?0000 RSN=0000+
               0000',ROUTCDE=11,MF=L
WTOTBEND EQU   *
         DS    0D
WTOTBSZE EQU   *-WTOTABLE
*---------------------------------------------------------------------*
*        Dynamic Storage Area                                         *
*--------+---------+---------+---------+---------+---------+---------+*
DSA      DSECT ,
DSASAVE  DS    18F                     mainline savearea
DSASBSA  DS    18F                     subroutine savearea
         DS    0X
DSAFLG1  DS    XL1                     flags
DSA@RV0  EQU   X'80'                   -available
DSA@RV1  EQU   X'40'                   -available
DSA@RV2  EQU   X'20'                   -available
DSA@RV3  EQU   X'10'                   -available
DSA@RV4  EQU   X'08'                   -available
DSA@RV5  EQU   X'04'                   -available
DSA@RV6  EQU   X'02'                   -available
DSA@RV7  EQU   X'01'                   -available
         DS    0X
DSAVARID DS    CL1                     variable ID
DSAFLGSX DS    XL2                     -available
         DS    0F
DSAIPAR  DS    F                       input PARM reg
DSATXLN  DS    F                       WTO text length
DSAROUT  DS    F                       ROUTCDE & DESC codes
DSAEKEY  DS    F                       caller protect key
DSAESPN  DS    F                       storage subpool
         DS    0F
DSAOPAR  DS    0F                      output parms
DSARC    DS    F                       -R15 = RC
DSARSN   DS    F                       -R0  = RSN
DSAGR1   DS    F                       -R1  = output parm
DSA@OPZL EQU   *-DSAOPAR               keep it together
         DS    0C
DSAMID   DS    CL8                     message ID
         DS    0F
DSASRC   DS    XL4                     variable source
DSASRC2  DS    XL4                     variable source
DSASRCL  EQU   *-DSASRC                variable length
DSAVAR   DS    CL8                     variable data
         DS    CL1                     slack byte
DSAVARL  EQU   *-DSAVAR                variable length
         DS    0D
DSADWRD  DS    D                       conversion workarea
DSAPACK  DS    PL4                     conversion workarea
DSAPAK2  DS    PL4                     conversion workarea
DSA@PSZE EQU   *-DSADWRD
         DS    0F
DSAWTO   DS    0CL124                  WTO message workarea
DSAWLEN  DS    AL2                     -text length
         DS    AL2                     -MCS flags
DSAWID   DS    0CL7                    -message ID
DSAWSUB  DS    CL4                     -subsystem name
         DS    CL3                     -message number
         DS    CL1                     -blank
DSAWTXT  DS    CL112                   -msg text
DSAWTXTL EQU   *-DSAWTXT
DSAWTOL  EQU   *-DSAWTO                WTO workarea size
         DS    0F
DSAXTXT  DS    CL112                   compress text workarea
DSAXTXTL EQU   *-DSAXTXT               compress text workarea size
         DS    0D
DSA@SIZE EQU   *-DSA                   DSA size
*---------------------------------------------------------------------*
*        Server data areas                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         PRINT NOGEN
         ZSVT  ,
         ZRWA  ,
         ZGEQU ,
*---------------------------------------------------------------------*
*        System data areas                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         PRINT NOGEN
         IHAPSA ,
         DCBD  DSORG=PS,DEVD=DA
         END
